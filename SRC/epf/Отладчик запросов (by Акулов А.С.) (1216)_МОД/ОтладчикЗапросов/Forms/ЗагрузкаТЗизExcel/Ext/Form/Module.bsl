
Процедура ОткрытьНажатие(Элемент)
	Диалог = новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Фильтр = "Табличный документ (*.mxl)|*.mxl";
	Диалог.ПроверятьСуществованиеФайла = Истина;
	Если Диалог.Выбрать() Тогда
		ЭлементыФормы.ТабДок.Прочитать(Диалог.ПолноеИмяФайла);
	КонецЕсли;
КонецПроцедуры

Процедура КнопкаВыполнитьНажатие(Кнопка)
	Цифры = "0123456789";
	ДопустимыеСимволы = "АБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯабвгдеёжзийклмнопрстуфхцчшщъыьэюяABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz_"+Цифры;
	
	ТабДок = ЭлементыФормы.ТабДок;
	Если ТабДок.ТекущаяОбласть.Верх=ТабДок.ТекущаяОбласть.Низ или ТабДок.ВыделенныеОбласти.Количество()>1 Тогда
		Предупреждение("Выделите одну прямоугольную область для обработки!");
		ЭлементыФормы.Панель1.ТекущаяСтраница = ЭлементыФормы.Панель1.Страницы.ДанныеДляЗаполнения;
		Возврат;
	КонецЕсли;
	ТЗ = Новый ТаблицаЗначений;
	нс = ТабДок.ТекущаяОбласть.Верх;
	нкк = 1;
	Лево = ТабДок.ТекущаяОбласть.Лево;
	Право = ТабДок.ТекущаяОбласть.Право;
	СписокКолонок = Новый СписокЗначений;
	ТипКолонки = Новый ОписаниеТипов("Строка,Число", Новый КвалификаторыЧисла(20,6), Новый КвалификаторыСтроки(200, ДопустимаяДлина.Переменная));
	Для нк=Лево По Право Цикл
		ИмяКолонки=СокрЛП(ТабДок.Область(нс, нк).Текст);
		ПредставлениеКолонки = ИмяКолонки;
		сч = СтрДлина(ИмяКолонки)+1;
		Пока сч>1 Цикл
			сч = сч - 1;
			Если Найти(ДопустимыеСимволы, Сред(ИмяКолонки, сч, 1)) = 0 Тогда
				ИмяКолонки = Лев(ИмяКолонки, сч-1) + Сред(ИмяКолонки, сч+1);
			КонецЕсли;
		КонецЦикла;
		шк = ТабДок.Область(нс, нк).ШиринаКолонки;
		шк = ?(шк>0, шк, 20);
		Если ИмяКолонки="" или Найти(Цифры, Лев(ИмяКолонки, 1)) > 0  Тогда
			Кол = ТЗ.Колонки.Добавить(,ТипКолонки,ПредставлениеКолонки, шк);
		Иначе
			Кол = ТЗ.Колонки.Добавить(ИмяКолонки,ТипКолонки,ПредставлениеКолонки, шк);
		КонецЕсли;
		СписокКолонок.Добавить(нк-лево, Кол.Заголовок, Ложь);
	КонецЦикла;
	
	СписокКолонок.ОтметитьЭлементы("Числовые колонки:");
	
	Для нс=ТабДок.ТекущаяОбласть.Верх+1 по ТабДок.ТекущаяОбласть.Низ Цикл
		Строчка = ТЗ.Добавить();
		Для нк=Лево по Право Цикл
			нкк = нк-Лево;
			тЗнч = СокрЛП(ТабДок.Область(нс, нк).Текст);
			Если СписокКолонок.НайтиПоЗначению(нкк).Пометка Тогда
				Попытка
					ТекЗнч = Число(СтрЗаменить(СтрЗаменить(СтрЗаменить(тЗнч, Символы.НПП, ""), " ", ""), ",", "."));
				Исключение
					ТекЗнч = 0;
				КонецПопытки;
			Иначе
				ТекЗнч = тЗнч;
			КонецЕсли;
			Строчка.Установить(нкк, ТекЗнч);
		КонецЦикла;
	КонецЦикла;
	ЭлементыФормы.ТЗ.СоздатьКолонки();
	ЭлементыФормы.Панель1.ТекущаяСтраница = ЭлементыФормы.Панель1.Страницы.Результат;
	ЭлементыФормы.ОсновныеДействияФормы.Кнопки.ОК.Доступность = (ТЗ.Количество()>0);
	Для каждого Колонка Из ЭлементыФормы.ТЗ.Колонки Цикл
		Колонка.ОтображатьИтогиВПодвале = Истина;
	КонецЦикла;
КонецПроцедуры

Процедура ТЗПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	ТипЧисло = Тип("Число");
	Для каждого Ячейка Из ОформлениеСтроки.Ячейки Цикл
		Если ТипЗнч(ДанныеСтроки[Ячейка.Имя])=ТипЧисло Тогда
			Ячейка.ЦветФона = ЦветаСтиля.ЦветФонаПодсказки;
		Иначе
			Ячейка.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура КоманднаяПанель2Свернуть(Кнопка)
	ТипЧисло = Тип("Число");
	Если ТЗ.Количество()=0 или ТЗ.Колонки.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	СписокКолонокГр = Новый СписокЗначений;
	Для каждого Колонка Из ТЗ.Колонки Цикл
		СписокКолонокГр.Добавить(Колонка.Имя, Колонка.Заголовок, Ложь);
	КонецЦикла;
	СписокКолонокГр.ОтметитьЭлементы("Сгруппировать по:");
	тСписокКолонокГр = "";
	СписокКолонокСумм = Новый СписокЗначений;
	Для каждого Элемент Из СписокКолонокГр Цикл
		Если Элемент.Пометка Тогда
			тСписокКолонокГр = ?(тСписокКолонокГр="", "", тСписокКолонокГр+",")+Элемент.Значение;
		ИначеЕсли ТипЗнч(ТЗ[0][Элемент.Значение])=ТипЧисло Тогда
			СписокКолонокСумм.Добавить(Элемент.Значение, Элемент.Представление, Истина);
		КонецЕсли;
	КонецЦикла;
	СписокКолонокСумм.ОтметитьЭлементы("Суммировать по:");
	тСписокКолонокСумм = "";
	Для каждого Элемент Из СписокКолонокСумм Цикл
		Если Элемент.Пометка Тогда
			тСписокКолонокСумм = ?(тСписокКолонокСумм="", "", тСписокКолонокСумм+",")+Элемент.Значение;
		КонецЕсли;
	КонецЦикла;
	ТЗ.Свернуть(тСписокКолонокГр, тСписокКолонокСумм);
	ЭлементыФормы.ТЗ.СоздатьКолонки();
	Для каждого Колонка Из ЭлементыФормы.ТЗ.Колонки Цикл
		Колонка.ОтображатьИтогиВПодвале = Истина;
	КонецЦикла;
КонецПроцедуры

Процедура ОсновныеДействияФормыОК(Кнопка)
	Закрыть(ТЗ.Скопировать());
КонецПроцедуры

Процедура КоманднаяПанель2Сохранить(Кнопка)
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Фильтр = "Файл данных (*.dat)|*.dat";
	Если Диалог.Выбрать() Тогда
		ЗначениеВФайл(Диалог.ПолноеИмяФайла, ТЗ);
	КонецЕсли;
КонецПроцедуры

