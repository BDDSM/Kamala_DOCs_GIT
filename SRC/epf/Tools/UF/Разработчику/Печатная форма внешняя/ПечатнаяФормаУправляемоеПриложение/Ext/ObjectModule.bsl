#Область Регисстрация
// Стандартные Функции и Методы для  регистрации внешней печатной формы +

Функция СведенияОВнешнейОбработке() Экспорт
	
	ПараметрыРегистрации = Новый Структура;
	МассивНазначений = Новый Массив;
	
	Наименование = УказатьНаименованиеВнешнейПечатнойФормы();
	
	МассивНазначений.Добавить(ПолучитьМетаданныеДокументаПечати());
	//может быть - ЗаполнениеОбъекта, ДополнительныйОтчет, СозданиеСвязанныхОбъектов... 
	ПараметрыРегистрации.Вставить("Вид", "ПечатнаяФорма");
	ПараметрыРегистрации.Вставить("Назначение", МассивНазначений);
	//имя под которым обработка будет зарегестрирована в справочнике внешних обработок
	ПараметрыРегистрации.Вставить("Наименование", Наименование);
	ПараметрыРегистрации.Вставить("Версия", "1.1");
	ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);
	//так будет выглядеть описание печ.формы для пользователя
	ПараметрыРегистрации.Вставить("Информация", УказатьНаименованиеВнешнейПечатнойФормы());
	
	ТаблицаКоманд = ПолучитьТаблицуКоманд();
	ДобавитьКоманду(ТаблицаКоманд, Наименование, УказатьНаименованиеКомандыПечати(), "ВызовСерверногоМетода", Истина, "ПечатьMXL");
	
	ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

Функция ПолучитьТаблицуКоманд()
	
	Команды = Новый ТаблицаЗначений;
	//как будет выглядеть описание печ.формы для пользователя
	Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	//имя макета печ.формы
	Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	//ВызовСерверногоМетода
	Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
	Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
	
	Возврат Команды;
КонецФункции

Функция УказатьНаименованиеВнешнейПечатнойФормы()
	Наименование = ЭтотОбъект.Метаданные().Представление();
	Возврат Наименование;
КонецФункции

Функция УказатьНаименованиеКомандыПечати()
	Наименование = ЭтотОбъект.Метаданные().ПолноеИмя();
	Возврат Наименование;
КонецФункции

Функция ПолучитьМетаданныеДокументаПечати()
	ПолноеИмя = Документ.Метаданные().ПолноеИмя();
	Возврат ПолноеИмя;
КонецФункции

Функция ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
	
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда. Представление = Представление;
	НоваяКоманда. Идентификатор= Идентификатор;
	НоваяКоманда. Использование= Использование;
	НоваяКоманда. ПоказыватьОповещение= ПоказыватьОповещение;
	НоваяКоманда. Модификатор= Модификатор;
	
КонецФункции

// Стандартные Функции и Методы для  регистрации внешней печатной формы -
#КонецОбласти

Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			УказатьНаименованиеКомандыПечати(),
			УказатьНаименованиеВнешнейПечатнойФормы(),
			ПечатьФормы(МассивОбъектов));
			
КонецПроцедуры
		
Функция ПечатьФормы(МассивОбъектов) Экспорт
	ТабДок = новый ТабличныйДокумент;
	
	Макет = ПолучитьМакет("Макет");
	
	ОбластьШапки   = Макет.ПолучитьОбласть("Шапка");
	ОбластьДанные  = Макет.ПолучитьОбласть("Данные");
	ОбластьПодвал  = Макет.ПолучитьОбласть("Подвал");
	
	Запрос = новый запрос;
		Запрос.УстановитьПараметр("МассивОбъектов",МассивОбъектов);
		Запрос.Текст = "ВЫБРАТЬ
		               |	РеализацияТоваровУслугТовары.Номенклатура,
		               |	РеализацияТоваровУслугТовары.Сумма,
		               |	РеализацияТоваровУслугТовары.Цена,
		               |	РеализацияТоваровУслугТовары.Количество,
		               |	РеализацияТоваровУслугТовары.Ссылка
		               |ИЗ
		               |	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
		               |ГДЕ
		               |	РеализацияТоваровУслугТовары.Ссылка В(&МассивОбъектов)";
		
	ОбщаяВыборка = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СсылкаНаОбъект из МассивОбъектов Цикл
		
		ОбластьШапки.Параметры.ТекстЗаголовка = "Печатная форма "+СсылкаНаОбъект.Номер;
		ОбластьШапки.Параметры.Организация = СсылкаНаОбъект.Организация;
		
		ТабДок.Вывести(ОбластьШапки);
		
		Отбор = Новый Структура;
		Отбор.Вставить("Ссылка",СсылкаНаОбъект);
		
		Выборка = ОбщаяВыборка.НайтиСтроки(Отбор);
		
		ИтогоСумма      = 0;
		ИтогоКоличество = 0;
		
		Для Каждого Стр из Выборка Цикл
			ЗаполнитьЗначенияСвойств(ОбластьДанные.Параметры,Стр);
			
			ИтогоСумма = ИтогоСумма + Стр.Сумма;
			ИтогоКоличество = ИтогоКоличество + Стр.Количество;
			
			ТабДок.Вывести(ОбластьДанные);
		КонецЦикла;
		
		ОбластьПодвал.Параметры.ИтогоКоличество = ИтогоКоличество;
		ОбластьПодвал.Параметры.ИтогоСумма      = ИтогоСумма;
		
		ТабДок.Вывести(ОбластьПодвал);
		
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЦикла;
				   
	возврат ТабДок;
КонецФункции

