
// -= СОЗДАНИЕ ДЕРЕВА ПОДСИСТЕМ =-

Процедура ЗаполнитьУровниДерева(пРодительскаяВетка,пПодсистемы)
	Для Каждого Подсистема Из пПодсистемы Цикл
	    ДочерняяВетка                 = пРодительскаяВетка.Строки.Добавить();
		ДочерняяВетка.ПсПредставление = ?(Подсистема.Синоним = "",Подсистема.Имя,Подсистема.Синоним);
		ДочерняяВетка.Пометка         = 1;
		ДочерняяВетка.ПсПолноеИмя     = Подсистема.ПолноеИмя();
		ДочерняяВетка.ПсКартинка      = ЭлементыФормы.КартинкаПодсистема.Картинка;
		ЗаполнитьУровниДерева(ДочерняяВетка,Подсистема.Подсистемы);
	КонецЦикла;
КонецПроцедуры

Процедура ЗаполнитьДеревоПодсистем()
	ДеревоПодсистем.Строки.Очистить();
	Корень                 = ДеревоПодсистем.Строки.Добавить();
	Корень.ПсПредставление = "Все подсистемы";
	Корень.Пометка         = 1;
	Корень.ПсПолноеИмя     = "";
	Корень.ПсКартинка      = ЭлементыФормы.КартинкаПодсистемы.Картинка;
    ЗаполнитьУровниДерева(Корень,Метаданные.Подсистемы);
КонецПроцедуры	

Процедура ПриОткрытии()
	ЗаполнитьДеревоПодсистем();
КонецПроцедуры

// -= УПРАВЛЕНИЕ ДЕРЕВОМ ПОДСИСТЕМ =-

Процедура ПодсистемыПриИзмененииФлажка(Элемент, Колонка)
	ТекСтрока = Элемент.ТекущиеДанные;
	Если ТекСтрока.Пометка = 2 Тогда
		ТекСтрока.Пометка = 0;
	КонецЕсли;
	ИзменитьПометкиПодчиненных(ТекСтрока);
	ИзменитьПометкиВладельцев(ТекСтрока);
КонецПроцедуры

Процедура ДействияРазвернутьДерево(Кнопка)
	ЭлементыФормы.Подсистемы.Развернуть(ДеревоПодсистем.Строки[0],Истина);
КонецПроцедуры

Процедура ДействияСвернутьДерево(Кнопка)
	ЭлементыФормы.Подсистемы.Свернуть(ДеревоПодсистем.Строки[0]);
КонецПроцедуры

// -= ВЫБОР И ВОЗВРАТ ОТМЕЧЕННЫХ ПОДСИСТЕМ =-

Процедура ВыбратьОтмеченныеПодсистемыУровня(пРодительскаяВетка,пМассивПодсистем)
	Для Каждого Подсистема Из пРодительскаяВетка.Строки Цикл
		Если Подсистема.Пометка = 1 Тогда
			пМассивПодсистем.Добавить(Метаданные.НайтиПоПолномуИмени(Подсистема.ПсПолноеИмя));
		ИначеЕсли Подсистема.Пометка = 2 Тогда
			ВыбратьОтмеченныеПодсистемыУровня(Подсистема,пМассивПодсистем);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры	

Функция ВыбратьОтмеченныеПодсистемы()	
	МассивПодсистем = Новый Массив;
	Корень          = ДеревоПодсистем.Строки[0];
	ВыбратьОтмеченныеПодсистемыУровня(Корень,МассивПодсистем);
	Возврат МассивПодсистем;
КонецФункции	

Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	// Получаем отмеченные подсистемы дерева и передаем в главную форму
	ОтмеченныеПодсистемы = ВыбратьОтмеченныеПодсистемы();
	ЭтаФорма.ОповеститьОВыборе(ОтмеченныеПодсистемы);
КонецПроцедуры