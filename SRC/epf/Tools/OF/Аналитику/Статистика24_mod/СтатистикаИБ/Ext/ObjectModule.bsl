
// -= РАБОТА С ДЕРЕВОМ МЕТАДАННЫХ =-

Функция ОпределитьПометкуВладельца(пГлавный) Экспорт
	Подчиненные = пГлавный.Строки;
	УстановленныхОтметок = 0;
	СерыхОтметок = 0;
	СнятыхОтметок = 0;
	Для Каждого Подчиненный Из Подчиненные Цикл
		Если Подчиненный.Пометка = 0 Тогда
			СнятыхОтметок = СнятыхОтметок + 1;
		ИначеЕсли Подчиненный.Пометка = 1 Тогда
			УстановленныхОтметок = УстановленныхОтметок + 1;
		ИначеЕсли Подчиненный.Пометка = 2 Тогда	
			СерыхОтметок = СерыхОтметок + 1;
		КонецЕсли;
	КонецЦикла;
	
	// Если есть хоть одна серая отметка у подчиненных
	// то она будет и у главного
	Если СерыхОтметок > 0 Тогда
		Возврат 2;
	Иначе
		Если (СнятыхОтметок = 0) и (УстановленныхОтметок > 0) Тогда
			// Все отметки установлены, установлена отметка для главного
			Возврат 1;
		ИначеЕсли (СнятыхОтметок > 0) и (УстановленныхОтметок = 0) Тогда
			// Все отметки сняты, снята отметка для главного
			Возврат 0;
		ИначеЕсли (СнятыхОтметок > 0) и (УстановленныхОтметок > 0) Тогда
			// Частично отметки установлены, частично сняты, серая отметка для главного
			Возврат 2;
		КонецЕсли;
	КонецЕсли;
КонецФункции	

Процедура ИзменитьПометкиВладельцев(пПодчиненный) Экспорт	
	Подчиненный = пПодчиненный;
	Главный = Подчиненный.Родитель;
	Пока НЕ (Главный = Неопределено) Цикл
		Главный.Пометка = ОпределитьПометкуВладельца(Главный);
		Подчиненный = Главный;
		Главный = Подчиненный.Родитель;
	КонецЦикла;
КонецПроцедуры	

Процедура ИзменитьПометкиПодчиненных(пГлавный) Экспорт
	Подчиненные = пГлавный.Строки;
	Для Каждого Подчиненный Из Подчиненные Цикл
		Подчиненный.Пометка = пГлавный.Пометка;
		ИзменитьПометкиПодчиненных(Подчиненный);
	КонецЦикла;
КонецПроцедуры