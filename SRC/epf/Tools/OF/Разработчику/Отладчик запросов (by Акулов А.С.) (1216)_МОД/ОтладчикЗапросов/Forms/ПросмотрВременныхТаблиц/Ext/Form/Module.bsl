Перем ВременныеТаблицы Экспорт;
Перем РежимСтруктуры Экспорт;
Перем глЗапрос Экспорт;
Перем СтруктураЗапроса Экспорт;
Перем ТекстЗапроса Экспорт;

Процедура СоздатьСписокИзДереваИмен(ДЗ, СЗ, ТекИмя="")	
	Перем Строчка, тИмя;
	Для каждого Строчка Из ДЗ.Строки Цикл
		тИмя = ?(ТекИмя="", "", ТекИмя+".")+Строчка.Имя;
		СЗ.Добавить(Строчка, тИмя);
		СоздатьСписокИзДереваИмен(Строчка, СЗ, тИмя);
	КонецЦикла;
КонецПроцедуры

Процедура ПриОткрытии()
	СЗ = ЭлементыФормы.ВременнаяТаблица.СписокВыбора;
	СЗ.Очистить();
	Если РежимСтруктуры=Истина Тогда
		ТабЗнач.Очистить();
		ТабЗнач.Колонки.Очистить();
		Заголовок = "Анализ структуры запроса";
		Попытка
			СтруктураЗапроса.Колонки.Добавить("Данные");
			СтруктураЗапроса.Колонки.Добавить("Время");
		Исключение
		КонецПопытки;
		ЭлементыФормы.Сформировать.Видимость = Истина;
		ЭлементыФормы.НадписьВремя.Видимость = Истина;
		ЭлементыФормы.ОсновныеДействияФормы1.Свертка = РежимСверткиЭлементаУправления.Нет;
		ЭлементыФормы.ОсновныеДействияФормы1.Кнопки.СформироватьВсе.Доступность = Истина;
		ЭлементыФормы.ОсновныеДействияФормы1.Кнопки.ТекстЗапроса.Доступность = Истина;
		СоздатьСписокИзДереваИмен(СтруктураЗапроса, СЗ);
		ВременнаяТаблица = СЗ[0].Значение;
	Иначе
		ЭлементыФормы.Сформировать.Видимость = Ложь;
		ЭлементыФормы.НадписьВремя.Видимость = Ложь;
		ЭлементыФормы.ОсновныеДействияФормы1.Свертка = РежимСверткиЭлементаУправления.Лево;
		ЭлементыФормы.ОсновныеДействияФормы1.Кнопки.СформироватьВсе.Доступность = Ложь;
		ЭлементыФормы.ОсновныеДействияФормы1.Кнопки.ТекстЗапроса.Доступность = Ложь;
		Заголовок = "Просмотр временных таблиц";
		Для каждого Строчка из ВременныеТаблицы Цикл
			СЗ.Добавить(Строчка.НомерЗапроса, ""+Строчка.Псевдоним+"  (Запрос № "+Формат(Строчка.НомерЗапроса, "ЧН=; ЧГ=")+")");
		КонецЦикла;
		ВременнаяТаблица = СЗ[0].Значение;
		ВременнаяТаблицаПриИзменении(Неопределено);
	КонецЕсли;
КонецПроцедуры

Процедура ВременнаяТаблицаПриИзменении(Элемент)
	Если РежимСтруктуры=Истина Тогда
		Если ?(ТипЗнч(ВременнаяТаблица)=Тип("СтрокаДереваЗначений"), ТипЗнч(ВременнаяТаблица.Данные)=Тип("ТаблицаЗначений"), Ложь) Тогда
			ЭлементыФормы.Сформировать.Заголовок = "Обновить";
			ТабЗнач = ВременнаяТаблица.Данные.Скопировать();
			ЭлементыФормы.НадписьВремя.Заголовок = ВременнаяТаблица.Время;
			ЭлементыФормы.ТабЗнач.СоздатьКолонки();
		Иначе
			ЭлементыФормы.НадписьВремя.Заголовок = "";
			ЭлементыФормы.Сформировать.Заголовок = "Сформировать";
			ТабЗнач.Очистить();
			ТабЗнач.Колонки.Очистить();
		КонецЕсли;
	Иначе
		Стр = ВременныеТаблицы.Найти(ВременнаяТаблица, "НомерЗапроса");
		ЭлементыФормы.ТабЗнач.Видимость = (Стр<>Неопределено);
		Если Стр<>Неопределено Тогда
			ТабЗнач = Стр.Данные.Скопировать();
			ЭлементыФормы.ТабЗнач.СоздатьКолонки();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ВременнаяТаблицаАвтоПодборТекста(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка)
	Перем т, П, ЭлементВыбора;
	т = врег(Текст);
	СтандартнаяОбработка = Ложь;
	СЗ = Новый СписокЗначений;
	Для каждого ЭлементВыбора Из ЭлементыФормы.ВременнаяТаблица.СписокВыбора Цикл
		П = Найти(врег(ЭлементВыбора.Представление), т);
		Если П>0 Тогда
			СЗ.Добавить(П, ЭлементВыбора.Представление);
		КонецЕсли;
	КонецЦикла;
	Если СЗ.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	СЗ.СортироватьПоЗначению();
	ЭлементВыбора = ЭтаФорма.ВыбратьИзСписка(СЗ, Элемент, СЗ[0]);
	Если ЭлементВыбора<>Неопределено Тогда
		т = ЭлементВыбора.Представление;
		Для каждого ЭлементВыбора Из ЭлементыФормы.ВременнаяТаблица.СписокВыбора Цикл
			Если т=ЭлементВыбора.Представление Тогда
				ВременнаяТаблица = ЭлементВыбора.Значение;
				ТекстАвтоПодбора = ВременнаяТаблица;
				ВременнаяТаблицаПриИзменении(Неопределено);
				Возврат;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Процедура ВременнаяТаблицаОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	ВременнаяТаблицаАвтоПодборТекста(Элемент, Текст, Значение, СтандартнаяОбработка);
КонецПроцедуры

Процедура ВременнаяТаблицаНачалоВыбора(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если РежимСтруктуры=Истина Тогда
		фрм = ПолучитьФорму("СтруктураЗапроса");
		фрм.СтруктураЗапроса = СтруктураЗапроса;
		фрм.ТекстЗапроса = ТекстЗапроса;
		Ответ = фрм.ОткрытьМодально();
		Если Ответ=Неопределено Тогда
			Возврат;
		КонецЕсли;
		Ответ.Удалить("ЭтоПодзапрос");
		ПСтр = СтруктураЗапроса.Строки.НайтиСтроки(Ответ, Истина);
		Если Пстр=Неопределено Тогда
			Возврат;
		ИначеЕсли ПСтр.Количество()<>1 Тогда
			Возврат;
		КонецЕсли;
		ВременнаяТаблица = ПСтр[0];
		ВременнаяТаблицаПриИзменении(Неопределено);
	Иначе
		Форма = ПолучитьФорму("ФормаВыбораИзСписка");
		Форма.Заголовок = "Выберите врем.таблицу:";
		Форма.СЗ = ЭлементыФормы.ВременнаяТаблица.СписокВыбора;
		Если Форма.ОткрытьМодально()=Истина Тогда
			ВременнаяТаблица = Форма.ВыбраннаяСтрока.Значение;
			ВременнаяТаблицаПриИзменении(Неопределено);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьТекущийТекстЗапроса(ВТ)
	Перем тЗапроса;
	Если РежимСтруктуры<>Истина Тогда
		Возврат "";
	КонецЕсли;
	Если ТипЗнч(ВТ)<>Тип("СтрокаДереваЗначений") Тогда
		Возврат "";
	КонецЕсли;
	тЗапроса = Сред(ТекстЗапроса, ВТ.НачПозиция, ВТ.КонПозиция-ВТ.НачПозиция+1);
	П = ПарсерТекстаЗапроса.НайтиЗакрывающуюСкобку(тЗапроса, 1);
	Если П>0 Тогда
		Если ВТ.ЭтоПодзапрос Тогда
			тЗапроса = "("+тЗапроса;
		Иначе		
			тЗапроса = Лев(тЗапроса, П-2);
		КонецЕсли;
	КонецЕсли;
	Если ВТ.Вид="Источник" Тогда
		Возврат "ВЫБРАТЬ
				|	*
				|ИЗ "+тЗапроса;
	Иначе
		Возврат тЗапроса;
	КонецЕсли;
КонецФункции

Функция ВыполнитьВремЗапрос(ВТ)
 	тЗапроса = ПолучитьТекущийТекстЗапроса(ВТ);
	Если тЗапроса="" Тогда
		Возврат Ложь;
	КонецЕсли;
	ВремЗапрос = Новый Запрос;
	ВремЗапрос.МенеджерВременныхТаблиц = глЗапрос.МенеджерВременныхТаблиц;
	ВремЗапрос.Текст = тЗапроса;
	Для каждого Параметр Из глЗапрос.Параметры Цикл ВремЗапрос.Параметры.Вставить(Параметр.Ключ, Параметр.Значение); КонецЦикла;
	врм1 = глозПолучитьТекущееВремяВМиллисекундах();
	ВТ.Данные = ВремЗапрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.Прямой);
	врм2 = глозПолучитьТекущееВремяВМиллисекундах();
	ВремяВыполнения = (врм2-врм1);
	Если ВремяВыполнения>1000 Тогда
		ВремяЗапроса = Дата(1,1,1) + Цел(ВремяВыполнения / 1000);
		ВремяЗапроса = СокрП(Формат(ВремяЗапроса, "ДФ=ЧЧ:мм:сс','"))+СокрЛ(Формат(ВремяВыполнения % 1000, "ЧЦ=3; ЧДЦ=0; ЧН=; ЧГ="));
	Иначе
		ВремяЗапроса = "0,"+СокрЛ(Формат(ВремяВыполнения % 1000, "ЧЦ=3; ЧДЦ=0; ЧН=; ЧГ="));
	КонецЕсли;
	ВТ.Время = ВремяЗапроса;
	Возврат Истина;
КонецФункции

Процедура СформироватьНажатие(Элемент)
	Если РежимСтруктуры Тогда
		Если ВыполнитьВремЗапрос(ВременнаяТаблица) Тогда
			ВременнаяТаблицаПриИзменении(Элемент);
		КонецЕсли;
	Иначе
		ЭлементыФормы.Сформировать.Видимость = Ложь;
		ЭлементыФормы.НадписьВремя.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

Процедура ТекстНажатие(Элемент)
	Если РежимСтруктуры Тогда
		ОчиститьСообщения();
		Сообщить(ПолучитьТекущийТекстЗапроса(ВременнаяТаблица), СтатусСообщения.БезСтатуса);
	Иначе
		ЭлементыФормы.Сформировать.Видимость = Ложь;
		ЭлементыФормы.НадписьВремя.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

Процедура ОсновныеДействияФормы1СформироватьВсе(Кнопка)
	Для каждого Элемент Из ЭлементыФормы.ВременнаяТаблица.СписокВыбора Цикл
		Состояние("Выполнение: "+Элемент.Представление);
		ВыполнитьВремЗапрос(Элемент.Значение);
		ВременнаяТаблицаПриИзменении(Элемент);
	КонецЦикла;
	Состояние();
КонецПроцедуры

Процедура ТабЗначПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

Процедура КоманднаяПанель1ПроизвольнаяСортировка(Кнопка)
	Форма = ПолучитьФорму("НастройкаСортировкиТЗ", ЭтаФорма, ЭтаФорма);
	Если Форма.Открыта() Тогда
		Форма.Закрыть();
	КонецЕсли;
	Форма.НачальноеЗаполнение(ТабЗнач);
	СтрокаСортировки = Форма.ОткрытьМодально();
	Если СтрокаСортировки<>Неопределено и не ПустаяСтрока(СтрокаСортировки) Тогда
		ТабЗнач.Сортировать(СтрокаСортировки);
	КонецЕсли;
КонецПроцедуры

