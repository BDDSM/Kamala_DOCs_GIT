
Процедура НеограниченнаяПриИзменении(Элемент)
	ЭлементыФормы.Длина2.Доступность = не Неограниченная;
КонецПроцедуры

Процедура КПРазвернутьВсе(Кнопка)
		ЭлементыФормы.ДеревоТипов.Видимость = Ложь;
	Попытка
		Для каждого Строчка из ДеревоТипов.Строки Цикл
			ЭлементыФормы.ДеревоТипов.Развернуть(Строчка);
		КонецЦикла;
		ЭлементыФормы.ДеревоТипов.Видимость = Истина;
	Исключение
		ЭлементыФормы.ДеревоТипов.Видимость = Истина;
		ВызватьИсключение;
	КонецПопытки;
КонецПроцедуры

Процедура КПСвернутьВсе(Кнопка)
	ЭлементыФормы.ДеревоТипов.Видимость = Ложь;
	Попытка
		Для каждого Строчка из ДеревоТипов.Строки Цикл
			ЭлементыФормы.ДеревоТипов.Свернуть(Строчка);
		КонецЦикла;
		ЭлементыФормы.ДеревоТипов.Видимость = Истина;
	Исключение
		ЭлементыФормы.ДеревоТипов.Видимость = Истина;
		ВызватьИсключение;
	КонецПопытки;
КонецПроцедуры

Процедура ДеревоТиповПриАктивизацииСтроки(Элемент)
	Перем Стр;
	Если НастраиватьТип Тогда
		Стр = ЭлементыФормы.ПанельНастройки.Страницы.Найти(ЭлементыФормы.ДеревоТипов.ТекущиеДанные.Представление);
		ЭлементыФормы.ПанельНастройки.ТекущаяСтраница = ?(Стр=Неопределено, ЭлементыФормы.ПанельНастройки.Страницы.Пустая, Стр);
	КонецЕсли;
КонецПроцедуры

Процедура ДеревоТиповВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	ПринятьИЗакрыть(Элемент);
КонецПроцедуры

Процедура ПринятьИЗакрыть(Кнопка)
	Если ЭлементыФормы.ДеревоТипов.ТекущиеДанные.Родитель = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ВыбЗнач = ЭлементыФормы.ДеревоТипов.ТекущиеДанные.Тип;
	Если ВыбЗнач.СодержитТип(Тип("Число")) Тогда
		ВыбЗнач = новый ОписаниеТипов(ВыбЗнач, , , Новый КвалификаторыЧисла(Длина, Точность, ?(Неотрицательное, ДопустимыйЗнак.Неотрицательный, ДопустимыйЗнак.Любой)));
	ИначеЕсли ВыбЗнач.СодержитТип(Тип("Строка")) Тогда
		ВыбЗнач = новый ОписаниеТипов(ВыбЗнач, , , , Новый КвалификаторыСтроки(?(Неограниченная, 0, Длина), ?(Неограниченная или ПеременнаяДлина, ДопустимаяДлина.Переменная, ДопустимаяДлина.Фиксированная)));
	ИначеЕсли ВыбЗнач.СодержитТип(Тип("Дата")) Тогда
		ВыбЗнач = новый ОписаниеТипов(ВыбЗнач, , , , , Новый КвалификаторыДаты(?(СоставДаты=1, ЧастиДаты.Дата, ?(СоставДаты=2, ЧастиДаты.Время, ЧастиДаты.ДатаВремя))));
	КонецЕсли;
	НеВыбрано = ложь;
	ЭтаФорма.Закрыть(истина);
КонецПроцедуры

Процедура ПриОткрытии()
	Перем тПростыеТипы, тЭлементыКонфигурации, сч, тСтр, тИндексКартинки;
	Перем ТекСтр, ТекТип, Родитель, Строка, тТип;
	ТекСтр = Неопределено;
	ВыбТип = ?(ВыбЗнач.Типы().Количество()=0, Неопределено, ВыбЗнач.Типы().Получить(0));
	Длина = Макс(ВыбЗнач.КвалификаторыЧисла.Разрядность, ВыбЗнач.КвалификаторыСтроки.Длина);
	Точность = ВыбЗнач.КвалификаторыЧисла.РазрядностьДробнойЧасти;
	Неотрицательное = (ВыбЗнач.КвалификаторыЧисла.ДопустимыйЗнак = ДопустимыйЗнак.Неотрицательный);
	Неограниченная = (ВыбЗнач.КвалификаторыСтроки.Длина=0);
	ПеременнаяДлина = (ВыбЗнач.КвалификаторыСтроки.ДопустимаяДлина = ДопустимаяДлина.Переменная);
	СоставДаты = ?(ВыбЗнач.КвалификаторыДаты.ЧастиДаты = ЧастиДаты.Дата, 1, ?(ВыбЗнач.КвалификаторыДаты.ЧастиДаты = ЧастиДаты.Время, 2, 0));
	ЭлементыФормы.Длина2.Доступность = не Неограниченная;
	тПростыеТипы = 
	"
	|0,Простые типы
	|20,Неопределено,Неопределено
	|1,Число,Число
	|2,Строка,Строка
	|3,Дата,Дата
	|4,Булево,Булево
	|3,Граница,Граница
	|3,МоментВремени,Момент времени
	|"+?(БезКоллекций, "", "
	|0,Коллекции
	|5,Массив,Массив
	|6,СписокЗначений,Список значений
	|7,ТаблицаЗначений,Таблица значений
	|");
	Для сч=1 по СтрЧислоСтрок(тПростыеТипы) Цикл
		тСтр = СокрЛП(СтрПолучитьСтроку(тПростыеТипы, сч));
		Если тСтр="" Тогда
			Продолжить;
		КонецЕсли;
		тСтр = СтрЗаменить(тСтр, ",", Символы.ПС);
		тИндексКартинки = Число(СтрПолучитьСтроку(тСтр, 1));
		Если тИндексКартинки = 0 Тогда
			Родитель = ДеревоТипов.Строки.Добавить();
			Родитель.Картинка = 0;
			Родитель.Представление = СтрПолучитьСтроку(тСтр, 2);
		Иначе
			Строка = Родитель.Строки.Добавить();
			Строка.Картинка = тИндексКартинки;
			Строка.Представление = СтрПолучитьСтроку(тСтр, 3);
			Строка.Тип = новый ОписаниеТипов(СокрЛП(СтрПолучитьСтроку(тСтр, 2)));
			тТип = ?(Строка.Тип.Типы().Количество()=1, Строка.Тип.Типы().Получить(0), Неопределено);
			Если (Строка.Тип=ВыбЗнач) или (тТип=ВыбТип) Тогда
				ТекСтр = Строка;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	тЭлементыКонфигурации = "	
	|8,Справочники,Справочники
	|9,Документы,Документы
	|10,Перечисления,Перечисления
	|11,ПланыВидовХарактеристик,Планы видов харрактеристик
	|12,ПланыСчетов,Планы счетов
	|13,ПланыВидовРасчета,Планы видов расчета
	|14,БизнесПроцессы,Бизнес процессы
	|15,ТочкиМаршрутаБизнесПроцесса,Точки маршрута бизнес процесса,ТочкаМаршрутаБизнесПроцессаСсылка,БизнесПроцессы
	|16,ПланыВидовРасчета,Планы видов расчета
	|17,Задачи,Задачи
	|18,ПланыОбмена,Планы обмена
	//|19,Харрактеристики,Харрактеристики,ХаррактеристикаСсылка,ПланыВидовХарактеристик
	|";
	Для сч=1 по СтрЧислоСтрок(тЭлементыКонфигурации) Цикл
		тСтр = СокрЛП(СтрПолучитьСтроку(тЭлементыКонфигурации, сч));
		Если тСтр="" Тогда
			Продолжить;
		КонецЕсли;
		тСтр = СтрЗаменить(тСтр, ",", Символы.ПС);
		тИндексКартинки = Число(СтрПолучитьСтроку(тСтр, 1));
		тИмя = СокрЛП(СтрПолучитьСтроку(тСтр, 2));
		тЗамена = СокрЛП(СтрПолучитьСтроку(тСтр, 4));
		тИмя = ?(тЗамена="", тИмя, СокрЛП(СтрПолучитьСтроку(тСтр, 5)));
		МетаКоллекция = Метаданные[тИмя];
		Если МетаКоллекция.Количество()>0 Тогда
			Родитель = ДеревоТипов.Строки.Добавить();
			Родитель.Картинка = 0;
			Родитель.Представление = СтрПолучитьСтроку(тСтр, 3);
			Для Каждого МетаОбъект из МетаКоллекция Цикл
				Строка = Родитель.Строки.Добавить();
				Строка.Картинка = тИндексКартинки;
				Строка.Представление = МетаОбъект.Представление();
				тИмяТипа = ?(тЗамена="", СтрЗаменить(МетаОбъект.ПолноеИмя(), ".", "Ссылка."), тЗамена+"."+МетаОбъект.Имя);
				Строка.Тип = новый ОписаниеТипов(тИмяТипа);
				Если Строка.Тип=ВыбЗнач Тогда
					ТекСтр = Строка;
				КонецЕсли;
			КонецЦикла;
			Родитель.Строки.Сортировать("Представление");
		КонецЕсли;
	КонецЦикла;
	Если (ТекСтр<>Неопределено) и (не НеВыбрано) Тогда
		ЭлементыФормы.ДеревоТипов.ТекущаяСтрока = ТекСтр;
	КонецЕсли;
	ЭлементыФормы.КоманднаяПанель1.Кнопки.ВыбратьПустую.Доступность = не БезПустых;
КонецПроцедуры

Процедура КоманднаяПанель1ВыбратьПустую(Кнопка)
	НеВыбрано = истина;
	Закрыть(Истина);
КонецПроцедуры
