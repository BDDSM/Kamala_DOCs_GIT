Перем КодТекстЗапроса;
Перем ФормаНастройки;
Перем КодУниверсальногоОтчета;
Перем КодПостроителя;

Функция ПолучитьМассивПараметровЗапроса()
	Запрос = Новый Запрос(ТекстЗапроса);
	М = Запрос.НайтиПараметры();
	Возврат М;
КонецФункции

Процедура СформироватьТекстРедактораКода()
	Перем Код, МассивПараметров, Параметр, ИмяПараметра, ДопКод;
	Код = "";
	Если ЭлементыФормы.Вкладки.ТекущаяСтраница=ЭлементыФормы.Вкладки.Страницы.ТекстЗапроса Тогда
		Код = КодТекстЗапроса;
	ИначеЕсли ЭлементыФормы.Вкладки.ТекущаяСтраница=ЭлементыФормы.Вкладки.Страницы.КодЗапроса Тогда
		Код = "	Запрос = Новый Запрос();";
		МассивПараметров = ПолучитьМассивПараметровЗапроса();
		Для каждого Параметр Из МассивПараметров Цикл
			ИмяПараметра = Параметр.Имя;
			Код = Код + "
			|	Запрос.УстановитьПараметр("""+ИмяПараметра+""", "+ИмяПараметра+");"
		КонецЦикла;
		Код = Код + "
		|	Запрос.Текст = "+КодТекстЗапроса+";
		|
		|	// ТЗ = Запрос.Выполнить.Выгрузить(ОбходРезультатаЗапроса.Прямой);	
		|   //
		|	//Если ТЗ.Количество()=0 Тогда
		|	//	Предупреждение(""Нет данных."");
		|	//	Возврат;
		|	//КонецЕсли;
		|
		|	РезультатЗапроса = Запрос.Выполнить();
		|	Если РезультатЗапроса.Пустой() Тогда
		|		Предупреждение(""Нет данных."");
		|		Возврат;
		|	КонецЕсли;
		|
		|	Выборка1 = РезультатЗапроса.Выбрать();
		|	Пока Выборка1.Следующий() Цикл
		|
		|		Выборка2 = Выборка1.Выбрать();
		|		Пока Выборка2.Следующий() Цикл
		|
		|		КонецЦикла;
		|	КонецЦикла;
		|";
	ИначеЕсли ЭлементыФормы.Вкладки.ТекущаяСтраница=ЭлементыФормы.Вкладки.Страницы.КодПостроителя Тогда
		Код = "	ТабДок.Очистить();
		|	ПостроительОтчета = Новый ПостроительОтчета();
		|	ПостроительОтчета.Текст = "+КодТекстЗапроса+";
		|	ПостроительОтчета.ТекстЗаголовка = ""Заголовок построителя отчетов""; // <---------------------------------------- !!!";
		МассивПараметров = ПолучитьМассивПараметровЗапроса();
		Для каждого Параметр Из МассивПараметров Цикл
			ИмяПараметра = Параметр.Имя;
			Код = Код + "
			|	ПостроительОтчета.Параметры.Вставить("""+ИмяПараметра+""", "+ИмяПараметра+");";
		КонецЦикла;
		
		Код = Код + "
		|
		|	ПостроительОтчета.ЗаполнитьНастройки();
		|
		|	//	Донастроить построитель отчета
		|	//
		|"+?(КодПостроителя<>Неопределено, КодПостроителя, "//ПостроительОтчета.ДоступныеПоля
		|	//ПостроительОтчета.Отбор
		|	//ПостроительОтчета.ИзмеренияСтроки
		|	//ПостроительОтчета.ИзмеренияКолонки
		|	//ПостроительОтчета.Порядок
		|   //
		|	//ПостроительОтчета.ВыбранныеПоля")+"
		|
		|	ПостроительОтчета.РазмещениеИзмеренийВКолонках = ТипРазмещенияИзмерений.Отдельно;
		|	ПостроительОтчета.РазмещениеИзмеренийВСтроках = ТипРазмещенияИзмерений.Вместе;
		|
		|	ПостроительОтчета.МакетОформления = ПолучитьМакетОформления(СтандартноеОформление.Интерфейс); // <---------------- !!!
		|
		|	ПостроительОтчета.ОформитьМакет();
		|
		|	ПостроительОтчета.Выполнить();
		|
		|	ПостроительОтчета.Вывести(ТабДок);
		|	
		|	ТабДок.ТолькоПросмотр = Истина;
		|	ТабДок.Показать();
		|";
		
		//|   ПостроительОтчета.ДоступныеПоля..Представление = ;
	ИначеЕсли ЭлементыФормы.Вкладки.ТекущаяСтраница=ЭлементыФормы.Вкладки.Страницы.КодУниверсальногоОтчета Тогда
		Если КодУниверсальногоОтчета<>Неопределено Тогда
			Код = КодУниверсальногоОтчета;
		Иначе
			Код = СокрЛП(ПолучитьМакет("МакетКодаУниверсальногоОтчета").ПолучитьТекст());
			Код = СтрЗаменить(Код, "[{ТекстЗапроса}]", КодТекстЗапроса);
			ДопКод = "";
			МассивПараметров = ПолучитьМассивПараметровЗапроса();
			Для каждого Параметр Из МассивПараметров Цикл
				ИмяПараметра = Параметр.Имя;
				ДопКод = ДопКод + "
				|	УниверсальныйОтчет.ПостроительОтчета.Параметры.Вставить("""+ИмяПараметра+""", {"+ИмяПараметра+"});";
			КонецЦикла;
			Стр = "
				|	//...
				|	//...
				|	//...";
			Код = СтрЗаменить(Код, "[{УстановкаДополнительныхПараметров}]", ДопКод);
			Код = СтрЗаменить(Код, "[{ПредставлениеПолей}]", "//УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить(""ХарактеристикаНоменклатуры"", ""Характеристика номенклатуры"");"+Стр);
			Код = СтрЗаменить(Код, "[{Показатели}]", "//УниверсальныйОтчет.ДобавитьПоказатель(""КоличествоНачальныйОстаток"",""Количество"", Истина, ""ЧЦ=15; ЧДЦ=3"", ""НачальныйОстаток"", ""Начальный остаток"");"+Стр);
			Код = СтрЗаменить(Код, "[{ИзмеренияСтрок}]", "	//УниверсальныйОтчет.ДобавитьИзмерениеСтроки(""Номенклатура"");"+Стр);
			Код = СтрЗаменить(Код, "[{ИзмеренияКолонок}]", СокрЛП(Стр));
			Код = СтрЗаменить(Код, "[{Отборы}]", "//УниверсальныйОтчет.ДобавитьОтбор(""Склад"");"+Стр);
			Код = СтрЗаменить(Код, "[{Упорядочивание}]", СокрЛП(Стр));
			Код = СтрЗаменить(Код, "[{ВидПериода}]", "");
			Код = СтрЗаменить(Код, "[{ОтрицательноеКрасным}]", "УниверсальныйОтчет.ОтрицательноеКрасным = Истина;");
			Код = СтрЗаменить(Код, "[{ВыводитьОбщиеИтоги}]", "");
			Код = СтрЗаменить(Код, "[{ВыводитьДетальныеЗаписи}]", "");
			Код = СтрЗаменить(Код, "[{ВыбиратьИспользованиеСвойств}]", "УниверсальныйОтчет.мВыбиратьИспользованиеСвойств = Ложь;");
			Код = СтрЗаменить(Код, "[{ИспользоватьСвойстваИКатегории}]", "УниверсальныйОтчет.ИспользоватьСвойстваИКатегории = Ложь;");
			Код = СтрЗаменить(Код, "[{РежимФормыНастройкиБезГруппировокКолонок}]", "УниверсальныйОтчет.мРежимФормыНастройкиБезГруппировокКолонок = Ложь;");
		КонецЕсли;
	КонецЕсли;
	ЭлементыФормы.РедакторКода.УстановитьТекст(Код);
КонецПроцедуры

Процедура Панель1ПриСменеСтраницы(Элемент, ТекущаяСтраница)
	СформироватьТекстРедактораКода();
	ЭлементыФормы.КомандыУО.Видимость = (ТекущаяСтраница=2) или (ТекущаяСтраница=3);
КонецПроцедуры

Процедура ПриОткрытии()
	СтрПС = Символы.ПС+Символы.Таб+"|";
	КодТекстЗапроса = """"+СтрЗаменить(СтрЗаменить(ТекстЗапроса,"""",""""""), Символы.ПС, СтрПС)+"""";
	СформироватьТекстРедактораКода();
КонецПроцедуры

Процедура КомандыУОНастроить(Кнопка)
	Если ФормаНастройки=Неопределено Тогда
		ФормаНастройки = ПолучитьФорму("КонструкторУО");
		ФормаНастройки.Инициализировать(ТекстЗапроса);
	КонецЕсли;
	Если ФормаНастройки.ОткрытьМодально()=Истина Тогда
		КодУниверсальногоОтчета = ФормаНастройки.ПолучитьКод();
		КодПостроителя = ФормаНастройки.ПолучитьКодПостроителя();
		СформироватьТекстРедактораКода();
	КонецЕсли;
КонецПроцедуры

ФормаНастройки = Неопределено;
КодУниверсальногоОтчета = Неопределено;
КодПостроителя = Неопределено;
КодТекстЗапроса = "";